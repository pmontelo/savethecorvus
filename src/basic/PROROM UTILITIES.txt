 10  HIMEM: 36864: REM $9000
 20  REM CALLING VECTORS
 30 C1 = 36867: REM READ
 40 C2 = C1 + 3: REM WRITE
 50 C3 = C2 + 3: REM INIT
 60  REM ZERO PAGE
 70 CB% = 0: REM CONFIG BLOCK 0-5
 80 DR% = 24: REM DRIVE NUMBER
 90 VL% = 25: REM VOLUME NUMBER
 100 ST% = 31: REM CALL STATUS / CONFIG OFFSET
 110  REM DEFINE VARIABLES
 120 D$ =  CHR$ (4)
 130 BL$ =  CHR$ (7)
 140 ER$ = BL$ + BL$ + BL$
 150 CR$ =  CHR$ (13)
 160 TM% = 34: REM TOP MARGIN
 170  REM 
 180  PRINT : TEXT : HOME :
 190  PRINT "---------------------------------------"
 200  PRINT "-                                     -"
 210  PRINT "-       CORVUS PROROM UTILITIES       -"
 220  PRINT "- COPYRIGHT 1984-2025 BY PAT MONTELO  -"
 230  PRINT "-                                     -"
 240  PRINT "---------------------------------------"
 250  POKE TM%,6: PRINT D$"BRUN CPD.0"
 260  HOME 
 270  PRINT "C)ONFIGURE SYSTEM"
 280  PRINT "I)NITALIZE DOS VOLUMES"
 290  PRINT "P)RODOS SETUP"
 300  PRINT "Q)UIT"
 310  PRINT ":";
 320  GET IN$
 330  IF IN$ = "Q" THEN TEXT : END 
 340  IF IN$ = "C" THEN 410
 350  IF IN$ = "I" THEN 750
 360  IF IN$ = "P" THEN 380
 370  PRINT BL$;: GOTO 320
 380  HOME : PRINT "PLEASE FLIP THE DISK AND PRESS RETURN";
 390  GET IN$: IF IN$ <  > CR$ THEN 390
 400  PRINT : PRINT "PLEASE REBOOT": END 
 410  HOME : REM CONFIGURE
 420  REM CONFIGURE BLOCK OFFSET 
 430 CB = CB%
 440  PRINT "DRIVE ONE-": GOSUB 480
 450 CB = CB% + 3
 460  PRINT "DRIVE TWO-": GOSUB 480: GOTO 680
 470  REM 
 480  PRINT "06 MEG = 11358" : REM At least, could be larger, mine died lol
 490  PRINT "11 MEG = 24320" : REM IMI 5012H = $5F00 512 byte blocks, 12,451,840 bytes 
 500  PRINT "20 MEG = ?????" : REM Sorry, I never owned one
 510  INPUT "TOTAL NUMBER OF 512 BLOCKS:";TB
 520 MB% = TB / 256
 530 ML = TB - MB% * 256
 540  IF MB% > 255 OR ML > 255 THEN  PRINT "NUMBER OUT OF RANGE": GOTO 480
 550  REM COMPUTE SECTOR COUNT
 560 SC = TB * 2
 570  REM COMPUTE MAX VOLUMES
 580 MV% = SC / (49 * 16)
 590 MV% = MV% - 1: IF MV% < 0 THEN MV% = 0
 600  PRINT "NUMBER OF DOS 3.3 784 SECTOR VOLUMES"
 610  PRINT "MAXIMUM = "MV%
 620  INPUT "VOLUMES:";VL
 630  IF VL > MV% OR VL < 0 THEN  PRINT "OUT OF RANGE": GOTO 620
 640  REM REDUCE PRODOS TOTAL BLOCKS BY VOLUMES * 49 TRACKS * 8 BLOCKS
 650 DB% = VL * 49 * 8:TB = TB - DB%:MB% = TB / 256:ML = TB - MB% * 256
 660  POKE CB,ML: POKE CB + 1,MB%: POKE CB + 2,VL
 670  RETURN 
 680  PRINT "CONFIGURING PROROM"
 690  CALL C2: REM WRITE
 700  IF  PEEK (ST%) <  > 0 THEN  PRINT ER$"ERROR CAN'T CONFIGURE": END 
 710  PRINT "CONFIGURE COMPLETE"
 720  PRINT "PLEASE FORMAT FOR PRODOS WITH FILER"
 730  PRINT "AND FOR DOS 3.3 WITH VOLUME INIT"
 740  PRINT "PRESS ANY KEY TO RETURN TO THE MENU:";: GET IN$: GOTO 260
 750  HOME : REM INIT
 760  PRINT "INIT VOLUMES"
 770  PRINT "DRIVE:";
 780  GET DR$: IF DR$ <  > "1" AND DR$ <  > "2" THEN 780
 790 DR =  VAL (DR$)
 800  PRINT DR
 810  CALL C1: REM READ
 820 CB = CB%: IF DR$ = "2" THEN CB = CB% + 3
 830 V% =  PEEK (CB + 2)
 840  PRINT V%" VOLUMES ON DRIVE "DR$
 850  INPUT "FIRST VOLUME TO INIT:";FV
 860  INPUT "LAST VOLUME TO INIT:";LV
 870  IF LV < 1 OR LV > V% OR LV < (FV) THEN  PRINT "OUT OF RANGE": GOTO 850
 880  INPUT "ARE YOU SURE?";IN$
 890  IF IN$ <  > "Y" THEN  PRINT "ABORTED": END 
 900  FOR VL = FV TO LV
 910  POKE DR%,DR: POKE VL%,VL
 920  PRINT "INITIALIZING VOLUME:"VL
 930  CALL C3: REM INIT
 940  IF  PEEK (ST%) <  > 0 THEN  PRINT ER$"ERROR CAN'T INIT VOLUME:"VL", STATUS:" PEEK (ST%): END 
 950  NEXT VL
 960  PRINT "INIT COMPLETE"
 970  PRINT "PRESS ANY KEY TO RETURN TO THE MENU";: GET IN$: GOTO 260
